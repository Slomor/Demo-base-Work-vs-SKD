&НаКлиенте
Перем ФормаВыбораКонтактногоЛица, ФормаВыбораПолучателя;

&НаСервереБезКонтекста
Функция ПолучитьКонтактноеЛицоПоЭлектроннойПочте(ЭлектроннаяПочта)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ КонтактноеЛицо ИЗ Справочник.Контрагенты ГДЕ ЭлектроннаяПочта = &ЭлектроннаяПочта";
	Запрос.Параметры.Вставить("ЭлектроннаяПочта", СокрЛП(ЭлектроннаяПочта));
	Выборка = Запрос.Выполнить().Выбрать();
	КонтактноеЛицо = "";
	Если Выборка.Следующий() Тогда
		КонтактноеЛицо = Выборка.КонтактноеЛицо;
	КонецЕсли;
	Возврат КонтактноеЛицо;
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьКонтактноеЛицоПоПолучателю(Получатель)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ КонтактноеЛицо ИЗ Справочник.Контрагенты ГДЕ Ссылка = &Получатель";
	Запрос.Параметры.Вставить("Получатель", Получатель);
	Выборка = Запрос.Выполнить().Выбрать();
	КонтактноеЛицо = "";
	Если Выборка.Следующий() Тогда
		КонтактноеЛицо = Выборка.КонтактноеЛицо;
	КонецЕсли;
	Возврат КонтактноеЛицо;
КонецФункции

&НаСервереБезКонтекста
Процедура ДобавитьПолучателей(Получатель, Получатели)	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ЭлектроннаяПочта ИЗ Справочник.Контрагенты ГДЕ Ссылка " ;
	Если ТипЗнч(Получатели) = Тип("Массив") Тогда
		Запрос.Текст = Запрос.Текст + "В (&Получатели)";
	Иначе
		Запрос.Текст = Запрос.Текст + "= &Получатели";
	КонецЕсли;
	Запрос.Параметры.Вставить("Получатели", Получатели);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Получатель <> "" Тогда
			Получатель = Получатель + "; ";
		КонецЕсли;
		Получатель = Получатель + Выборка.ЭлектроннаяПочта;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Ключ.Пустая() Тогда
		Заголовок = "Исходящее письмо (Создание)";
		Объект.Дата = ТекущаяДата();
		ВходящееПисьмо = Параметры.ВходящееПисьмо;
		Если Не ВходящееПисьмо.Пустая() Тогда
			РаботаСПочтой.ЗаполнитьОтветНаПисьмо(ВходящееПисьмо, Объект, Содержимое);
		КонецЕсли;
		Адресаты = Параметры.Адресаты;
		Если Адресаты <> Неопределено Тогда
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			               |	Контрагенты.ЭлектроннаяПочта
			               |ИЗ
			               |	Справочник.Контрагенты КАК Контрагенты
			               |ГДЕ
			               |	Контрагенты.Ссылка В(&Адресаты)
			               |	И Контрагенты.ЭлектроннаяПочта <> """"";
			Запрос.УстановитьПараметр("Адресаты", Адресаты);			   
			Получатель = "";
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				Если Получатель <> "" Тогда
					Получатель = Получатель + "; ";
				КонецЕсли;
				Получатель = Получатель + Выборка.ЭлектроннаяПочта;
			КонецЦикла;
			Объект.Получатель = Получатель;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Содержимое = ТекущийОбъект.Содержимое.Получить();
	Заголовок = ТекущийОбъект.Наименование + " (Исходящее письмо)";
	Если  РаботаСПочтой.ПисьмоОтправлено(ТекущийОбъект.Ссылка) Тогда
		Заголовок = Заголовок + " - Отправлено";
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекущийОбъект.Содержимое = Новый ХранилищеЗначения(Содержимое, Новый СжатиеДанных());
	ТекущийОбъект.Текст = Содержимое.ПолучитьТекст();
КонецПроцедуры

&НаСервере
Функция ОтправитьПисьмо()
	Записать();
	Если Не РаботаСПочтой.ОтправитьПисьмо(Объект.Ссылка) Тогда
		Возврат Ложь;
	КонецЕсли;
	Заголовок = Заголовок + " - Отправлено";
	Возврат Истина;
КонецФункции

&НаКлиенте
Функция ОтправитьПисьмоКлиент()
	Если Не ОтправитьПисьмо() Тогда
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(1, "Настроить почту");
		Кнопки.Добавить(2, "Закрыть");
		
		Если Вопрос("Не указаны настройки интернет почты!", Кнопки,, 1) = 1 Тогда
			ОткрытьФорму("ОбщаяФорма.НастройкаПочты");
		КонецЕсли;
		Возврат Ложь;
	КонецЕсли;
	
	НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(Объект.Ссылка);
	ПоказатьОповещениеПользователя("Письмо отправлено", НавигационнаяСсылка, Объект.Наименование);
	ОповеститьОбИзменении(Объект.Ссылка);
	Возврат Истина;
КонецФункции

&НаКлиенте
Процедура Отправить(Команда)
	ОтправитьПисьмоКлиент();
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьИЗакрыть(Команда)
	Если Не ОтправитьПисьмоКлиент() Тогда
		Возврат;
	КонецЕсли;
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ВставитьСтрокуВТекущуюПозицию(Поле, Документ, Строка)
	Перем Начало, Конец;
	Поле.ПолучитьГраницыВыделения(Начало, Конец);
	Документ.Удалить(Начало, Конец);
	Документ.Вставить(Начало, Строка);
	Позиция = Документ.ПолучитьПозициюПоЗакладке(Начало) + СтрДлина(Строка);
	Закладка = Документ.ПолучитьЗакладкуПоПозиции(Позиция);
	Поле.УстановитьГраницыВыделения(Закладка, Закладка);
КонецПроцедуры


&НаКлиенте
Процедура ВставитьКонтактноеЛицо(Команда)
	Если Объект.Получатель <> "" Тогда
		КонтактноеЛицо = ПолучитьКонтактноеЛицоПоЭлектроннойПочте(Объект.Получатель);
		Если КонтактноеЛицо <> "" Тогда
			ВставитьСтрокуВТекущуюПозицию(Элементы.Содержимое, Содержимое, КонтактноеЛицо + " ");
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	П = Новый Структура("РежимВыбора", Истина);
	ФормаВыбораКонтактногоЛица = ОткрытьФорму("Справочник.Контрагенты.ФормаВыбора", П, ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	Если ИсточникВыбора = ФормаВыбораПолучателя Тогда
		Получатель = Элементы.Получатель.ТекстРедактирования;
		ДобавитьПолучателей(Получатель, ВыбранноеЗначение);
		Объект.Получатель = Получатель;
	ИначеЕсли ИсточникВыбора = ФормаВыбораКонтактногоЛица Тогда
		КонтактноеЛицо = ПолучитьКонтактноеЛицоПоПолучателю(ВыбранноеЗначение);
		ВставитьСтрокуВТекущуюПозицию(Элементы.Содержимое, Содержимое, КонтактноеЛицо + " ");
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура ПолучательНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	П = Новый Структура("РежимВыбора, МножественныйВыбор", Истина, Истина);
	ФормаВыбораПолучателя = ОткрытьФорму("Справочник.Контрагенты.ФормаВыбора", П, ЭтаФорма);
КонецПроцедуры


&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	Заголовок = ТекущийОбъект.Наименование + " (Исходящее письмо)";
КонецПроцедуры

