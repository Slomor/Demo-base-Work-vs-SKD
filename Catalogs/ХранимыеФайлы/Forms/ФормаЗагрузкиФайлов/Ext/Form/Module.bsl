
//////////////////////////////////////////////////////////////////////////////// 
// ОБРАБОТЧИКИ СОБЫТИЙ
//

&НаКлиенте
Процедура СписокФайловПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = Истина;
	ДобавитьФайлы();
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьФайлы()
	
	ВыборФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ВыборФайла.МножественныйВыбор = Истина;
	ВыборФайла.Заголовок = НСтр("ru = 'Выбор файла'", "ru");
	ВыборФайла.Фильтр = НСтр("ru = 'Все файлы'", "ru") + " (*.*)|*.*";
	ВыборФайла.ПредварительныйПросмотр = Истина;
	Если ВыборФайла.Выбрать() Тогда
		Для каждого Файл Из ВыборФайла.ВыбранныеФайлы Цикл
			СписокФайлов.Добавить(Файл);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКаталог()
	
	ВыборФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Если ВыборФайла.Выбрать() Тогда
		Файлы = НайтиФайлы(ВыборФайла.Каталог, "*.*", Истина);
		Для каждого Файл Из Файлы Цикл
			Если Файл.ЭтоФайл() Тогда
				СписокФайлов.Добавить(Файл.ПолноеИмя);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Загрузить()
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	Если СписокФайлов.Количество() = 0 Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru = 'Файлы не выбраны'", "ru");
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	ПомещаемыеФайлы = Новый Массив;
	Для каждого Файл Из СписокФайлов Цикл
		Описание = Новый ОписаниеПередаваемогоФайла(Файл.Значение, "");
		ПомещаемыеФайлы.Добавить(Описание);
	КонецЦикла;
	
	ПомещенныеФайлы = Новый Массив;
	Если ПоместитьФайлы(ПомещаемыеФайлы, ПомещенныеФайлы, , Ложь) Тогда
		
		Для каждого Файл Из ПомещенныеФайлы Цикл
			СписокЗагруженныхФайлов.Добавить(Файл);
		КонецЦикла;
		Закрыть(Истина);
		
	Иначе
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru = 'Файлы не загружены'", "ru");
		Сообщение.Сообщить();
		
	КонецЕсли;

КонецПроцедуры
