//////////////////////////////////////////////////////////////////////////////// 
// ПРОЦЕДУРЫ И ФУНКЦИИ
//

// Процедура устанавливает отбор по контрагенту в списке документов журнала
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Нет.
&НаКлиенте
Процедура УстановитьОтборПоКонтрагенту()

	УдалитьОтборПоКонтрагенту();
	ЭлементОтбора = ЖурналДокументовСписок.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение    = Новый ПолеКомпоновкиДанных("Контрагент");
	ЭлементОтбора.ВидСравнения     = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.Использование    = Истина;
	ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ЭлементОтбора.ПравоеЗначение   = КонтрагентОтбор;

КонецПроцедуры

// Процедура отменяет отбор по контрагенту в списке документов журнала
//
// Параметры:
//  Нет.
//
// Возвращаемое значение: 
//  Нет.
&НаКлиенте
Процедура УдалитьОтборПоКонтрагенту()

	ЭлементыОтбора = ЖурналДокументовСписок.Отбор.Элементы;
	ПолеКомпоновки = Новый ПолеКомпоновкиДанных("Контрагент");

	Для каждого ЭлементОтбора Из ЭлементыОтбора Цикл

		Если ЭлементОтбора.ЛевоеЗначение = ПолеКомпоновки Тогда
			ЭлементыОтбора.Удалить(ЭлементОтбора);
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

//////////////////////////////////////////////////////////////////////////////// 
// ОБРАБОТЧИКИ СОБЫТИЙ
//

&НаКлиенте
Процедура КонтрагентОтборПриИзменении(Элемент)

	Если НЕ КонтрагентОтбор.Пустая() Тогда
		ИспользоватьОтборПоКонтрагенту = Истина;
	КонецЕсли;

	Если ИспользоватьОтборПоКонтрагенту Тогда
		УстановитьОтборПоКонтрагенту();
		Элементы.ЖурналДокументовСписок.Обновить();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОтборПоКонтрагентуПриИзменении(Элемент)

	Если ИспользоватьОтборПоКонтрагенту	 Тогда
		УстановитьОтборПоКонтрагенту();
	Иначе
		УдалитьОтборПоКонтрагенту();
	КонецЕсли;

	Элементы.ЖурналДокументовСписок.Обновить();

КонецПроцедуры
