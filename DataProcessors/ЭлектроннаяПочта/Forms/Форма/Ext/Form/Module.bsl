&НаСервереБезКонтекста
Функция ПолучитьПоследнееВходящееПисьмо()
		//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВходящиеПисьма.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ВходящиеПисьма КАК ВходящиеПисьма
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			МАКСИМУМ(ВходящиеПисьма.Дата) КАК Дата
		|		ИЗ
		|			Справочник.ВходящиеПисьма КАК ВходящиеПисьма) КАК Т
		|		ПО ВходящиеПисьма.Дата = Т.Дата
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка";

	Результат = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	Если НЕ ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат Справочники.ВходящиеПисьма.ПустаяСсылка();
	КонецЕсли;
	Возврат ВыборкаДетальныеЗаписи.Ссылка;
КонецФункции

&НаСервере
Функция ПолучитьПисьма(Количество)
	Профиль = РаботаСПочтой.ПолучитьПрофиль();
	Если Профиль = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	Количество = РаботаСПочтой.ПолучитьНовыеПисьма(Профиль);
	Если Количество <> 0 Тогда
		Элементы.СписокВходящие.ТекущаяСтрока = ПолучитьПоследнееВходящееПисьмо();
	КонецЕсли;
	Возврат Истина;
КонецФункции	

&НаКлиенте
Процедура КомандаПолучитьПисьма(Команда)
	Количество = 0;
	Если Не ПолучитьПисьма(Количество) Тогда
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(1, "Настроить почту");
		Кнопки.Добавить(2, "Закрыть");
		
		Если Вопрос("Не указаны настройки интернет почты!", Кнопки,, 1) = 1 Тогда
			ОткрытьФорму("ОбщаяФорма.НастройкаПочты");
		КонецЕсли;
		Возврат;
	КонецЕсли;
	ПоказатьОповещениеПользователя("Загружено писем: " + Количество);
КонецПроцедуры

&НаКлиенте
Процедура КомандаНовоеПисьмо(Команда)
	ОткрытьФорму("Справочник.ИсходящиеПисьма.ФормаОбъекта", , ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтветить(Команда)
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВходящие Тогда
		Письмо = Элементы.СписокВходящие.ТекущаяСтрока;
	Иначе
		Письмо = Элементы.СписокИсходящие.ТекущаяСтрока;
	КонецЕсли;
	ПараметрыФормы = Новый Структура("ВходящееПисьмо", Письмо);
	ОткрытьФорму("Справочник.ИсходящиеПисьма.ФормаОбъекта",ПараметрыФормы, ЭтаФорма);
КонецПроцедуры


&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ОбновитьСписокВходящихПисем" Тогда
		Элементы.СписокВходящие.Обновить();
	КонецЕсли;
КонецПроцедуры

