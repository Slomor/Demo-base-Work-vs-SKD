
&НаКлиенте
Перем СоответствиеСертификатов;
&НаКлиенте
Перем ПараметрыВыбора;
&НаКлиенте
Перем ВыбраныйСертификат;

//////////////////////////////////////////////////////////////////////////////// 
// Общие процедуры и функции 
// 

// определяет типы хранилищ сертификатов, сертификаты которых требуется поместить в список
// ПараметрыВыбора_Вход - список типов хранилищ сертификатов
&НаКлиенте
Процедура Установка(ПараметрыВыбора_Вход) Экспорт
	ПараметрыВыбора = ПараметрыВыбора_Вход;
КонецПроцедуры

// возвращает результаты выбора в форме
// - при множественном выборе - массив сертификатов
// - при единичном выборе - выбранный сертификат криптографии
&НаКлиенте
Функция ПолучитьРезультатВыбора() Экспорт
	Если Параметры.МножественныйВыбор Тогда
		Вернуть = Новый Массив;
		Для Каждого СтрокаТаблициЗначений Из ТаблицаДляВыбора Цикл
			Если СтрокаТаблициЗначений.Выбран Тогда 
				Вернуть.Добавить(СоответствиеСертификатов[СтрокаТаблициЗначений]);
			КонецЕсли;
		КонецЦикла;
		Возврат Вернуть;
	Иначе
		Возврат ВыбраныйСертификат;
	КонецЕсли;
КонецФункции

//////////////////////////////////////////////////////////////////////////////// 
// ОБРАБОТЧИКИ СОБЫТИЙ 
// 

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	СоответствиеСертификатов = Новый Соответствие;
	// Заполнение таблицы сертификатов
	МенеджерКриптографии = Новый МенеджерКриптографии("", "", 75);
	Для Каждого Параметр Из ПараметрыВыбора Цикл
		Хранилище = МенеджерКриптографии.ПолучитьХранилищеСертификатов(Параметр.Значение);
		СертификатыХранилища = Хранилище.ПолучитьВсе();
		ПредставлениеПараметра = Параметр;
		
		ТекущаяДата = ТекущаяДата();
		Для Каждого Сертификат Из СертификатыХранилища Цикл
			Если Сертификат.ДатаОкончания < ТекущаяДата Тогда 
				Продолжить; // отфильтровываем истекшие сертификаты
			КонецЕсли;
			НоваяСтрока = ТаблицаДляВыбора.Добавить();
			СоответствиеСертификатов.Вставить(НоваяСтрока, Сертификат);
			НоваяСтрока.СертификатПредставление = Сертификат.Субъект.CN + НСтр("ru = ' выдан '", "ru") + Сертификат.Издатель.CN + НСтр("ru = ' действителен до '", "ru") + Сертификат.ДатаОкончания;
			НоваяСтрока.ТипХранилища = ПредставлениеПараметра;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.МножественныйВыбор Тогда
		Элементы.ОсуществлениеВыбора.Видимость = Ложь;
		ЭтаФорма.Заголовок = НСтр("ru = 'Список сертификатов получателей'", "ru");
	Иначе
		Элементы.ОК.КнопкаПоУмолчанию = Ложь;
		Элементы.ОсуществлениеВыбора.КнопкаПоУмолчанию = Истина;
		Элементы.ОК.Видимость = Ложь;
		Элементы.Отмена.Видимость = Ложь;
		Элементы.ТаблицаДляВыбораВыбран.Видимость = Ложь;
		ЭтаФорма.Заголовок = НСтр("ru = 'Сертификат для создания подписи'", "ru");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДляВыбораВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Не Параметры.МножественныйВыбор Тогда
		СтандартнаяОбработка = Ложь;
		Если Не ВыбраннаяСтрока = Неопределено Тогда 
			ВыбраныйСертификат = СоответствиеСертификатов[ ТаблицаДляВыбора[ВыбраннаяСтрока] ];
			ЭтаФорма.Закрыть(КодВозвратаДиалога.ОК);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОсуществлениеВыбораНажатие(Команда)
	Если Не Параметры.МножественныйВыбор Тогда
		ТекущиеДанные = Элементы.ТаблицаДляВыбора.ТекущиеДанные;
		Если Не ТекущиеДанные = Неопределено Тогда 
			ВыбраныйСертификат = СоответствиеСертификатов[ ТекущиеДанные ];
			ЭтаФорма.Закрыть(КодВозвратаДиалога.ОК);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
